unit Job.Model.Timer;

interface

uses
  System.SysUtils,
  System.Classes,
  System.SyncObjs;

type
  TTimerJobException =
    reference to procedure(Sender: TObject; AException: Exception);

  TTimerJob = class(TThread)
  private
    FEvent: TEvent;
    FOnTimer: TNotifyEvent;
    FInterval: Integer;
    FOnFinish: TNotifyEvent;
    FOnException: TTimerJobException;
    procedure SetOnTimer(const Value: TNotifyEvent);
    procedure SetInterval(const Value: Integer);
    procedure SetOnFinish(const Value: TNotifyEvent);
    procedure SetOnException(const Value: TTimerJobException);
  protected
    procedure Execute; override;

    procedure DoTimer;
    procedure DoFinish;
    procedure DoException(AException: Exception);
  public
    procedure AfterConstruction; override;
    procedure BeforeDestruction; override;
    procedure ForceTerminate;
    property Interval: Integer read FInterval write SetInterval;
    property OnTimer: TNotifyEvent read FOnTimer write SetOnTimer;
    property OnFinish: TNotifyEvent read FOnFinish write SetOnFinish;
    property OnException: TTimerJobException read FOnException write SetOnException;
  end;

implementation

{ TTimerJob }

procedure TTimerJob.AfterConstruction;
begin
  inherited;
  FInterval := 1000;
  FEvent := TEvent.Create;
end;

procedure TTimerJob.BeforeDestruction;
begin
  inherited;
  FEvent.Free;
end;

procedure TTimerJob.DoException(AException: Exception);
begin
  if Assigned(FOnException) then
  begin
    FOnException(Self, AException);
  end;
end;

procedure TTimerJob.DoFinish;
begin
  if Assigned(FOnFinish) then
  begin
    FOnFinish(Self);
  end;
end;

procedure TTimerJob.DoTimer;
begin
  if Assigned(FOnTimer) then
  begin
    FOnTimer(Self);
  end;
end;

procedure TTimerJob.Execute;
begin
  inherited;
  var LWaitResult: TWaitResult;
  try
    while not Self.Terminated do
    begin
      LWaitResult := FEvent.WaitFor(FInterval);

      if LWaitResult <> TWaitResult.wrTimeout then
      begin
        Exit;
      end;

      try
        Self.DoTimer;
      except
        on E: Exception do
        begin
          Self.DoException(E);
        end;
      end;
    end;
  finally
    Self.DoFinish;
  end;
end;

procedure TTimerJob.ForceTerminate;
begin
  FEvent.SetEvent;
end;

procedure TTimerJob.SetInterval(const Value: Integer);
begin
  FInterval := Value;
end;

procedure TTimerJob.SetOnException(const Value: TTimerJobException);
begin
  FOnException := Value;
end;

procedure TTimerJob.SetOnFinish(const Value: TNotifyEvent);
begin
  FOnFinish := Value;
end;

procedure TTimerJob.SetOnTimer(const Value: TNotifyEvent);
begin
  FOnTimer := Value;
end;

end.
